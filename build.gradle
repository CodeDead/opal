import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'application'
    id 'org.beryx.jlink' version '3.1.1'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group 'com.codedead'
version '1.5.0'

def currentOS = DefaultNativePlatform.currentOperatingSystem

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
        sourceCompatibility = JavaLanguageVersion.of(24)
        vendor = JvmVendorSpec.AMAZON
    }
}

tasks.named("updateDaemonJvm") {
    javaToolchains {
        vendor = JvmVendorSpec.AMAZON
    }
}

application {
    mainModule = 'Opal'
    mainClass = 'com.codedead.opal.OpalApplication'
    applicationDefaultJvmArgs = [
            '--enable-native-access', 'javafx.graphics,javafx.media'
    ]
}

javafx {
    version = '24.0.1'
    configuration = 'implementation'
    modules = ['javafx.base', 'javafx.controls', 'javafx.fxml', 'javafx.media']
}

jlink {
    options = ['--strip-debug', '--compress', 'zip-9', '--no-header-files', '--no-man-pages']
    forceMerge('jackson')
    launcher {
        name = 'Opal'
    }

    if (currentOS.isWindows()) {
        jpackage {
            installerType = 'msi'
            icon = "${project.rootDir}/.msi/opal.ico"
            installerOptions = [
                    '--win-menu',
                    '--win-menu-group', 'CodeDead',
                    '--win-shortcut-prompt',
                    '--win-upgrade-uuid', '876c5464-9a66-4913-89a4-c63a4b8b4bb9',
                    '--win-help-url', 'https://codedead.com/contact',
                    '--win-dir-chooser',
                    '--copyright', 'Copyright (c) 2025 CodeDead',
                    '--description', 'Opal is a free and open-source JavaFX application that can play relaxing music in the background',
                    '--vendor', 'CodeDead',
                    '--license-file', 'LICENSE',
                    '--app-version', "${project.version.toString()}",
                    '--about-url', 'https://codedead.com'
            ]
        }
    } else if (currentOS.isLinux()) {
        jpackage {
            installerType = 'rpm'
            icon = "${project.rootDir}/src/main/resources/images/opal.png"
            installerOptions = [
                    '--linux-shortcut',
                    '--linux-package-name', 'opal-codedead',
                    '--linux-rpm-license-type', 'GPLv3',
                    '--copyright', 'Copyright (c) 2025 CodeDead',
                    '--description', 'Opal is a free and open-source JavaFX application that can play relaxing music in the background',
                    '--vendor', 'CodeDead',
                    '--license-file', 'LICENSE',
                    '--app-version', "${project.version.toString()}",
                    '--about-url', 'https://codedead.com'
            ]
        }
    } else if (currentOS.isMacOsX()) {
        jpackage {
            installerType = 'dmg'
            icon = "${project.rootDir}/src/main/resources/images/opal.png"
            installerOptions = [
                    '--mac-package-name', 'Opal',
                    '--mac-package-identifier', 'com.codedead.opal',
                    '--mac-app-category', 'public.app-category.music',
                    '--copyright', 'Copyright (c) 2025 CodeDead',
                    '--description', 'Opal is a free and open-source JavaFX application that can play relaxing music in the background',
                    '--vendor', 'CodeDead',
                    '--license-file', 'LICENSE',
                    '--app-version', "${project.version.toString()}",
                    '--about-url', 'https://codedead.com'
            ]
        }
    }
}

tasks.register('AppImage') {
    dependsOn jpackageImage
    doLast {
        copy {
            from '.AppImage/.AppDir'
            into layout.buildDirectory.dir("AppImage/Opal.AppDir")
        }
        copy {
            from layout.buildDirectory.dir("jpackage/Opal/")
            into layout.buildDirectory.dir("AppImage/Opal.AppDir/usr/")
        }
        copy {
            from '.AppImage/.AppDir/Opal.png'
            into layout.buildDirectory.dir("AppImage/Opal.AppDir/usr/lib")
        }
        def appCreate = providers.exec {
            commandLine 'sh', "${project.rootDir}/.AppImage/createAppImage.sh", "${project.version.toString()}"
        }
        println(appCreate.standardOutput.asText.get().toString())
        delete layout.buildDirectory.dir("AppImage/Opal.AppDir")
    }
}

configure(AppImage) {
    group = 'build'
    description = 'Create an AppImage after creating the image of the application'
}

def homeConfig = System.properties['user.home'] + '/.config/com.codedead.opal'
clean.doFirst {
    delete 'default.properties'
    delete 'license.pdf'
    delete 'help.pdf'
    delete 'logs'
    delete '.com.codedead.opal'
    delete "$homeConfig"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:2.24.3'
    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.1'
}

tasks.named('test') {
    useJUnitPlatform()
}
